         PRINT NOGEN
****************************************************************
*        FILENAME: OFFER10D.MLC                                *
*        AUTHOR  : Bill Qualls/z390 Adaption by Anthony Delosa *
*        SYSTEM  : z390 v1703                                  *
*        REMARKS : Produce list of course offerings for all    *
*                  semesters. Includes page break logic.       *
*                  Multipe level control break.                *
*                  Minor break (dept) is summary only          *
****************************************************************
         START 0
         YREGS
BEGIN    SUBENTRY
         WTO   'OFFER10D ... Begin execution'
         BAL   R10,SETUP
MAIN     EQU   *
         CLI   EOFSW,C'Y'
         BE    EOJ
         BAL   R10,PROCESS
         B     MAIN
EOJ      EQU   *
         BAL   R10,WRAPUP
         WTO   'OFFER10D ... Normal end of program'
         RETURN
****************************************************************
*        SETUP - Those things which happen one time only,      *
*                before any records are processed.             *
****************************************************************
SETUP    EQU   *
         ST    R10,SVSETUP
         OPEN  (OFFER,INPUT)
         OPEN  (REPORT,OUTPUT)
         BAL   R10,READ
         MVC   HOLDSEM,ISEM
         MVC   HOLDDEPT,IDEPT
         L     R10,SVSETUP
         BR    R10
****************************************************************
*        HDGS - Print headings.                                *
****************************************************************
HDGS     EQU   *
         ST    R10,SVHDGS
         AP    PGS,=P'1'            Add 1 to page count
         MVC   HDPGS,=X'40202120'   Edit pattern for page count
         ED    HDPGS,PGS            Move page count to heading
         MVC   HDSEM,HOLDSEM        Move semester 
         PUT   REPORT,FORMFEED
         PUT   REPORT,HD1
         PUT   REPORT,HD2
         PUT   REPORT,BLANKS
         PUT   REPORT,HD3
         PUT   REPORT,HD4
         PUT   REPORT,HD5
         ZAP   LNS,=P'0'            Reset line count to zero
         L     R10,SVHDGS
         BR    R10
****************************************************************
*        PROCESS - Those things which happen once per record.  *
****************************************************************
PROCESS  EQU   *
         ST    R10,SVPROC
         BAL   R10,CHKSEM           See if new semester
         BAL   R10,CHKDEPT          See if new department
         AP    #SEM,=P'1'           Count sections by semester
         AP    #DEPT,=P'1'          Count sections by department
         BAL   R10,READ
         L     R10,SVPROC
         BR    R10
****************************************************************
*        READ - Read a record.                                 *
****************************************************************
READ     EQU   *
         ST    R10,SVREAD
         GET   OFFER,IREC           Read a single offer record
         B     READX
ATEND    EQU   *
         MVI   EOFSW,C'Y'
READX    EQU   *
         L     R10,SVREAD
         BR    R10
****************************************************************
*        CHKSEM - Check for change in semester                 *
*                 (control break)                              *
****************************************************************
CHKSEM   EQU   *
         ST    R10,SVCHKSEM
         CLC   HOLDSEM,ISEM         Compare with current
         BE    CHKSEMX              Same semester, get out
         BAL   R10,ENDSEM           Process semester break
         MVC   HOLDSEM,ISEM         Update major break field
         MVC   HOLDDEPT,IDEPT       Update minor break field
CHKSEMX  EQU   *
         L     R10,SVCHKSEM
         BR    R10
****************************************************************
*        CHKDEPT - Check for change in department              *
*        (minor control break)                                 *
****************************************************************
CHKDEPT  EQU   *
         ST    R10,SVCHKDEP
         CLC   HOLDDEPT,IDEPT       Compare w/ current
         BE    CHKDEPTX             Same department, get out
         BAL   R10,ENDDEPT          Process department break
         MVC   HOLDDEPT,IDEPT       Update control break field
CHKDEPTX EQU   *
         L     R10,SVCHKDEP
         BR    R10
****************************************************************
*        ENDSEM - End semester                                 *
*                 (Process control break)                      *
*                 Show count of sections for this semester     *
*                 Force next semester to another page          *
****************************************************************
ENDSEM   EQU   *
         ST    R10,SVENDSEM
         BAL   R10,ENDDEPT          Change in semester implies
*                                   change in dept as well 
         PUT   REPORT,HD5
         AP    LNS,=P'1'
         MVC   OREC(40),BLANKS      
         MVC   ODEPT(5),=CL5'Total'
         MVC   O#DEPT,=X'40202120'  Edit mask
         ED    O#DEPT,#SEM
         BAL   R10,WRITE
         MVC   OREC(40),BLANKS
         ZAP   #SEM,=P'0'           Reset counter
         ZAP   LNS,MAXLNS           Force next sem. to new page
         L     R10,SVENDSEM
         BR    R10 
****************************************************************
*        ENDDEPT - End department                              *
*                  (Process minor control break)               *
*                  Print count of sections in department       *
*                  if that count is two or more.               *
*                  Regardless, skip a line.                    *
****************************************************************
ENDDEPT  EQU   *
         ST    R10,SVENDDEP
         BAL   R10,CHKLNS
         MVC   OREC(40),BLANKS
         MVC   ODEPT,HOLDDEPT       Department
         MVC   O#DEPT,=X'40202120'  Edit mask
         ED    O#DEPT,#DEPT         # departments
         BAL   R10,WRITE
         ZAP   #DEPT,=P'0'           Reset # departments
         L     R10,SVENDDEP
         BR    R10 
****************************************************************
*        CHKLNS - Check lines printed. Full page?              *
****************************************************************
CHKLNS   EQU   *
         ST    R10,SVCHKLNS
         CP    LNS,MAXLNS
         BL    CHKLNSX
         BAL   R10,HDGS
CHKLNSX  EQU   *
         L     R10,SVCHKLNS
         BR    R10
****************************************************************
*        WRITE - Write a single detail line.                   *
****************************************************************
WRITE    EQU   *
         ST    R10,SVWRITE
         PUT   REPORT,OREC          Write report line
         AP    LNS,=P'1'
         L     R10,SVWRITE
         BR    R10
****************************************************************
*        WRAPUP - Those things which happen one time only,     *
*                 after all records have been processed.       *
****************************************************************
WRAPUP   EQU   *
         ST    R10,SVWRAP
         BAL   R10,ENDSEM           Final control break
         CLOSE OFFER
         CLOSE REPORT
         WTO   'OFFER10D ... Course list on REPORT.TXT'
         L     R10,SVWRAP
         BR    R10
****************************************************************
*        Literals, if any, will go here                        *
****************************************************************
         LTORG
****************************************************************
*        File definitions                                      *
****************************************************************
OFFER    DCB   LRECL=16,RECFM=FT,MACRF=R,EODAD=ATEND,                  X
               DDNAME=OFFER,RECORD=IREC
REPORT   DCB   LRECL=40,RECFM=FT,MACRF=W,                              X
               DDNAME=REPORT
****************************************************************
*        RETURN ADDRESSES                                      *
****************************************************************
SVSETUP  DC    F'0'              SETUP
SVHDGS   DC    F'0'              HDGS
SVPROC   DC    F'0'              PROCESS
SVREAD   DC    F'0'              READ
SVFORM   DC    F'0'              FORMAT
SVWRITE  DC    F'0'              WRITE
SVWRAP   DC    F'0'              WRAPUP
SVCHKLNS DC    F'0'              CHKLNS
SVCHKSEM DC    F'0'              CHKSEM
SVCHKDEP DC    F'0'              CHKDEP
SVENDSEM DC    F'0'              ENDSEM
SVENDDEP DC    F'0'              ENDDEP
****************************************************************
*        Miscellaneous field definitions                       *
****************************************************************
EOFSW    DC    CL1'N'            End of file? (Y/N)
PGS      DC    PL2'0'            Nbr of pages printed.
LNS      DC    PL2'10'           Lines printed on this page.
MAXLNS   DC    PL2'10'           Max nbr lines per page.
* My line counts exclude hdgs.
BLANKS   DC    CL40' '
HOLDSEM  DC    CL3' '            Hold semester
HOLDDEPT DC    CL2' '            Hold department
#SEM     DC    PL2'0'            Sem count
#DEPT    DC    PL2'0'            Department count
****************************************************************
*        Input record definition                               *
****************************************************************
IREC     DS    0CL18             1-18 Offer record
ISEM     DS    CL3               1- 3 Semester
ICID     DS    0CL5              4- 8 Course ID
IDEPT    DS    CL2               4- 5 Department
         DS    CL3               6- 8 Course number
ISECT    DS    CL1               9- 9 Section number
ITID     DS    CL3               10-12 Teacher ID
IROOM    DS    CL4               13-16 Room number
****************************************************************
*        Output (line) definition                              *
****************************************************************
OREC     DS    0CL40             1-40
         DC    CL10' '           1-10
ODEPT    DS    CL2               11-12 Department
         DC    CL7' '            13-19
O#DEPT   DS    CL4               20-23 Number of sections
         DC    CL17' '           24-40
****************************************************************
*        Headings definitions                                  *
****************************************************************
*
* ----+----1----+----2----+----3----+----4
*           COURSE OFFERINGS      PageBZZ9
*             Semester W92
*
*           Dept    Sections
*          ------   --------
*            XX       BZZ9
*            XX       BZZ9
*          ------   --------
*           Total     BZZ9
*
FORMFEED DS    0CL40
*         DC    X'0C' EBCDIC formfeed
*         DC    CL39' '
         DC    40C'_' For testing...
HD1      DS    0CL40
*                   ----+----1----+----2----+----3----+----4
         DC    CL36'          COURSE OFFERINGS      Page'
HDPGS    DC    CL4'BZZ9'
HD2      DS    0CL40
         DC    CL21'            Semester '
HDSEM    DS    CL3'XXX'
         DC    CL15' '   
HD3      DS    0CL40
         DC    CL40' '
HD4      DS    0CL40
         DC    CL40'          Dept    Sections              '
HD5      DS    0CL40
         DC    CL40'         ------   --------              '
         END   BEGIN