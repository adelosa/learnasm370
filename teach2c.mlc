         PRINT NOGEN
****************************************************************
* FILENAME: TEACH2C.MLC                                        *
* AUTHOR  : Bill Qualls. z390 adaption by Anthony Delosa       *
* SYSTEM  : z390 v1703                                         *
* REMARKS : List of teachers, with headings.                   *
****************************************************************
         START 0
         EQUREGS
BEGIN    SUBENTRY
         WTO   'TEACH2C ... Begin execution'
         OPEN  (TEACHERS,INPUT)
         OPEN  (REPORT,OUTPUT)
         PUT   REPORT,HD1
         PUT   REPORT,HD2
         PUT   REPORT,HD3
         PUT   REPORT,HD4
LOOP     GET   TEACHERS             Read a single teacher record
         MVC   OTID,ITID            Move teacher ID Nbr to output
         MVC   OTNAME,ITNAME        Move teacher Name to output
         MVC   OTDEG,ITDEG          Move highest degree to output
         MVC   OTTEN,ITTEN          Move tenure to output
         MVC   OTPHONE,ITPHONE      Move phone nbr to output
         PUT   REPORT,OREC          Write report line
         B     LOOP
*
* EOJ processing
*
ATEND    CLOSE TEACHERS
         CLOSE REPORT
         WTO   'TEACH2C ... Teacher list on REPORT.TXT'
         WTO   'TEACH2C ... Normal end of program'
         RETURN
*
* Literals, if any, will go here
*
         LTORG
*
* File definitions 
*
TEACHERS DCB   LRECL=27,RECFM=FT,MACRF=R,EODAD=ATEND,                  X
               DDNAME=TEACHER,RECORD=IREC
REPORT   DCB   LRECL=60,RECFM=FT,MACRF=W,                              X
               DDNAME=REPORT
*
* Miscellaneous field definitions
*
*
* Input record definition
*
IREC     DS    0CL27    Teacher record
ITID     DS    CL3      Teacher ID nbr
ITNAME   DS    CL15     Teacher name
ITDEG    DS    CL4      Highest degree
ITTEN    DS    CL1      Tenured?
ITPHONE  DS    CL4      Phone nbr
*
* Output (line) definition
*
OREC     DS    0CL60
OTID     DS    CL3      Teacher ID nbr
         DC    CL3' '
OTNAME   DS    CL15     Teacher name
         DC    CL3' '
OTDEG    DS    CL4      Highest degree
         DC    CL3' '
OTTEN    DS    CL1      Tenured?
         DC    CL3' '
OTPHONE  DS    CL4      Phone nbr
         DC    CL21' '
*
* Headings definitions
*
HD1      DS    0CL60
         DC    CL40'            LIST OF TEACHERS '
         DC    CL20' '
HD2      DS    0CL60
         DC    CL60' '
HD3      DS    0CL60
         DC    CL40'ID#        Name         Degr  Ten  Phone'
         DC    CL20' '
HD4      DS    0CL60
         DC    CL40'---   ---------------   ----  ---  -----'
         DC    CL20' '
         END   BEGIN 