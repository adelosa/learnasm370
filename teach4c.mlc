         PRINT NOGEN
****************************************************************
*        FILENAME: TEACH4C.MLC                                 *
*        AUTHOR  : Bill Qualls/z390 adoption by Anthony Delosa *
*        SYSTEM  : z390 v1703                                  *
*        REMARKS : This is a revision of TEACH4B.MLC.          *
*                  Produce list of tenured instructors.        *
*                  How to structure a BAL program.             *
*        ==AD==    Use PENTRY and PEXIT macros rather than BAS *
****************************************************************
         DCBD
         EQUREGS
         DECBD
         START 0
**************************************************************
*        MAINLINE
**************************************************************
BEGIN    SUBENTRY
         WTO   'TEACH4B.... Begin execution'
         PERFORM SETUP
MAIN     EQU   *
         CLI   EOFSW,C'Y'     IF END OF FILE
         BE    EOJ            GOTO EOJ
         PERFORM PROCESS
         B     MAIN           LOOP
EOJ      EQU   *
         PERFORM WRAPUP
         WTO   'TEACH4B ... Normal end of program'
         SUBEXIT
         RETURN
**************************************************************
*        SECTION: SETUP
**************************************************************
SETUP    PENTRY
         OPEN  (TEACHERS,INPUT)
         OPEN  (REPORT,OUTPUT)
         MVC   WTOTEXT(76),BLANK
         PERFORM HEADINGS
         PERFORM READ
         PEXIT
**************************************************************
*        SECTION: HEADINGS
**************************************************************
HEADINGS PENTRY
         PUT   REPORT,HD1
         PUT   REPORT,HD2
         PUT   REPORT,HD3
         PUT   REPORT,HD4
         PEXIT
**************************************************************
*        SECTION: PROCESS
**************************************************************
PROCESS  PENTRY
         CLI   ITTEN,C'Y'        Is Tenured?
         BNE   PROCNEXT          No, skip this record
         PERFORM FORMAT
         PERFORM WRITE
PROCNEXT PERFORM READ
         PEXIT
**************************************************************
*        SECTION: READ
**************************************************************
READ     PENTRY
         GET   TEACHERS
         B     READX
ATEND    EQU   *
         MVI   EOFSW,C'Y'
READX    PEXIT
**************************************************************
*        SECTION: FORMAT
**************************************************************
FORMAT   PENTRY
         MVC   OTID,ITID         Move teacher ID Nbr to output
         MVC   OTNAME,ITNAME     Move teacher Name to output
         CLC   ITDEG,=CL4'PHD'   If PHD?
         BE    YESPHD            ..
         MVI   OPHD,C'N'         ..
         B     OTHER
YESPHD   EQU   *
         MVI   OPHD,C'Y'         ..
OTHER    EQU   *
         MVC   OTPHONE+4,ITPHONE   Move phone nbr to output
         MVI   OTPHONE+3,HYPHEN
         MVC   OTPHONE(3),=C'517'
         PEXIT
**************************************************************
*        SECTION: WRITE
**************************************************************
WRITE    PENTRY
         MVC   WTOTEXT(60),OREC
         WTO   MF=(E,WTOBLOCK)
         PUT   REPORT,OREC
         PEXIT
**************************************************************
*        SECTION: WRAPUP
**************************************************************
WRAPUP   PENTRY
         CLOSE TEACHERS
         CLOSE REPORT
         WTO   'TEACH4C ... Teacher list on REPORT.TXT'
         PEXIT
**************************************************************
         LTORG
*
HYPHEN   EQU   C'-'
EOFSW    DC    CL1'N'         End of file switch
*
*        File definitions
*
TEACHERS DCB   LRECL=27,RECFM=FT,MACRF=R,EODAD=ATEND,                  X
               DDNAME=TEACHER,RECORD=IREC
REPORT   DCB   LRECL=60,RECFM=FT,MACRF=W,                              X
               DDNAME=REPORT
*
*        WTO block
*
WTOBLOCK EQU   *
         DC    H'80'         * For WTO, length of WTO buffer...
         DC    H'0'                     should be binary zeroes...
WTOTEXT  DS    CL76 
*
*        Input record definition
*
IREC     DS    0CL27          Teacher record
ITID     DS    CL3            Teacher ID nbr
ITNAME   DS    CL15           Teacher name
ITDEG    DS    CL4            Highest degree
ITTEN    DS    CL1            Tenured?
ITPHONE  DS    CL4            Phone nbr 
*
*        Heading definitions
*
HD1      DS    0CL60
         DC    CL40'      LIST OF TENURED INSTRUCTORS       '
         DC    CL20' '
HD2      DS    0CL60
         DC    CL60' '
HD3      DS    0CL60
         DC    CL40'ID#        Name         PhD?    Phone   '
         DC    CL20' '
HD4      DS    0CL60
         DC    CL40'---   ---------------   ----   -------- '
         DC    CL20' '
*
*        Record definition
*
OREC     DS    0CL60
OTID     DS    CL3            Teacher ID nbr
         DC    CL3' '
OTNAME   DS    CL15           Teacher name
         DC    CL4' '
OPHD     DS    CL1            PhD(Y/N)
         DC    CL5' '
OTPHONE  DS    CL8            Phone nbr
         DC    CL21' '
BLANK    DC    CL76' '        Blank record

         END