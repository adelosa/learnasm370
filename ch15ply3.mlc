         PRINT NOGEN
********************************************************************
*        CH15PL3.MLC
*        Chapter 15 Pg 15.6 Loading a table at runtime
*        Some enhancements to process list of titles in prog
*        At end of processing, the resulting counts will be printed
********************************************************************
         START 0
         YREGS
CH15PLY3 SUBENTRY
         LA    R8,ITITLES           Set address of input table
         LA    R5,(ITITLEX-ITITLES)/L'ITITLES
LOOP     EQU   *
         MVC   ITITLE,0(R8)         Move title to lookup
         PERFORM DOIT               Process title
         LA    R8,L'ITITLES(R8)     POINT TO NEXT title
         BCT   R5,LOOP              SUBTRACT 1 FROM ROW COUNTER
*                                   IF RESULT > ZERO THEN REPEAT
********************************************************************
*        Print the entries in the generated table
********************************************************************
         LA    R8,TITLES            Set address of genned table
         LH    R5,#TTLS             Set to number of entries
LOOP2    EQU   *
         MVC   PRINT,=CL80' '       Clear the line
         MVC   PRINT(6),0(R8)       Move the title
         MVC   PRINT+7(8),=X'4020202020202020'  Edit mask
         ED    PRINT+7(8),6(R8)     Move the count
         XPRNT PRINT,L'PRINT        ASSIST print
         LA    R8,L'TITLES(R8)      Go to next entry in table
         BCT   R5,LOOP2             Branch on count
         RETURN
********************************************************************
*        DOIT : Process the title passed in ITITLE
*               Add to TITLES table if new
*               Increment Title count if already present
********************************************************************
DOIT     PENTRY
         LA    R6,TITLES            Point R6 to start of table
         LH    R7,#TTLS             Nbr table entries to R7
         CH    R7,=H'0'             No entries yet?
         BE    NOTFOUND             Then add title to table
LOOK     EQU   *
         CLC   0(6,R6),ITITLE       Is table title EQ input?
         BE    FOUND                Yes - Increment count
         LA    R6,L'TITLES(R6)      No - Point to next row
         BCT   R7,LOOK              Repeat till all entries
         LH    R7,#TTLS             Reload actual nbr entries
NOTFOUND EQU   *                    Want to add title to table
         CH    R7,#MAXTTLS          Is table already full?
         BNL   TOOMANY              Yes - then message
         MVC   0(6,R6),ITITLE       No - then add title
         ZAP   6(4,R6),=P'1'        Initialize count to one
         LA    R7,1(R7)             Increment nbr entries
         STH   R7,#TTLS             and save
         B     LOOKED               Done with new entry
TOOMANY  EQU   *                    Table not large enough
         WTO   'Too many titles for table'
         RETURN
FOUND    EQU   *                    Found title in table
         AP    6(4,R6),=P'1'        Increment count for title
LOOKED   EQU   *                    Title has been processed
         PEXIT
         LTORG
********************************************************************
*        Input list
*        Each item is processed by the DOIT routine
********************************************************************
ITITLES  DS    0CL6
         DC    CL6'MR'
         DC    CL6'MRS'
         DC    CL6'MR'
         DC    CL6'MR'
         DC    CL6'MISS'
         DC    CL6'MSTR'
         DC    CL6'DR'
         DC    CL6'MR'
         DC    CL6'MR'
         DC    CL6'MRS'
         DC    CL6'MRS'
         DC    CL6'MR'
         DC    CL6'MR'
ITITLEX  EQU   *
*        Work fields
ITITLE   DS    CL6
#TTLS    DC    H'0'
#MAXTTLS DC    H'100'
********************************************************************
*        Titles table -- generated by program
********************************************************************
TITLES   DS    100CL10
* Positions 1-6 are title, CL6
* Positions 7-10 are count, PL4
PRINT    DC    CL80' '
         END   CH15PLY3